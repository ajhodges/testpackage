apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'signing'

group = 'org.testpackage'
version = '0.1.1-SNAPSHOT'

description = """Command line JUnit test runner for standalone testing"""

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {

    mavenCentral()
}
dependencies {
    compile group: 'org.fusesource.jansi', name: 'jansi', version: '1.11'
    compile group: 'junit', name: 'junit', version: '4.11'
    compile group: 'com.google.guava', name: 'guava', version: '15.0'
    compile group: 'org.slf4j', name: 'slf4j-nop', version: '1.7.5'
    compile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
    compile group: 'args4j', name: 'args4j', version: '2.0.26'
}

test {
    filter {
        includeTestsMatching 'org.testpackage.test.*'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
               name 'TestPackage'
               packaging 'jar'
               description 'Command line JUnit test runner for standalone testing'
               url 'https://github.com/testpackage/testpackage'

               scm {
                   url 'scm:git@github.com:testpackage/testpackage.git'
                   connection 'scm:git@github.com:testpackage/testpackage.git'
                   developerConnection 'scm:git@github.com:testpackage/testpackage.git'
               }

               licenses {
                   license {
                       name 'The Apache Software License, Version 2.0'
                       url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution 'repo'
                   }
               }

               developers {
                   developer {
                       id 'rnorth'
                       name 'Richard North'
                   }
               }
           }
        }
    }
}
